# For an example of how to make your own CMake project that uses OpenSim,
# see SampleCMakeLists.txt.
#
# This config file defines the following variables:
#   @CMAKE_PROJECT_NAME@_FOUND               True if search for libraries and headers
#                               succeeds.
#   @CMAKE_PROJECT_NAME@_ROOT_DIR            The installation directory.
#   @CMAKE_PROJECT_NAME@_LIB_DIR             The directory containing libraries.
#                               Suitable for link_directories().
#                               Also contains Simbody library directory.
#   @CMAKE_PROJECT_NAME@_INCLUDE_DIRS        Suitable for include_directories().
#                               Also contains Simbody include directory.
#   @CMAKE_PROJECT_NAME@_LIBRARIES           Suitable for target_link_libraries().
#
# Note that these variables also contain the Simbody include directories
# and libraries that OpenSim was built with, whether or not Simbody libraries and
# headers were copied into the OpenSim installation (CMake variable
# OPENSIM_COPY_SIMBODY).
#
# Adapted from SimbodyConfig.cmake
#

# To make the OpenSim installation relocatable:
@PACKAGE_INIT@

set_and_check(@CMAKE_PROJECT_NAME@_ROOT_DIR "@PACKAGE_CMAKE_INSTALL_PREFIX@")

# LIB_DIR
# -------
set_and_check(@CMAKE_PROJECT_NAME@_LIB_DIR
    "@PACKAGE_CMAKE_INSTALL_PREFIX@/@CMAKE_INSTALL_LIBDIR@")

if(NOT @OPENSIM_COPY_SIMBODY@)
    list(APPEND @CMAKE_PROJECT_NAME@_LIB_DIR "@Simbody_LIB_DIR@")
endif()

# INCLUDE_DIRS
# ------------
list(APPEND @CMAKE_PROJECT_NAME@_INCLUDE_DIRS
    "@PACKAGE_CMAKE_INSTALL_PREFIX@/sdk/include"
    "@PACKAGE_CMAKE_INSTALL_PREFIX@/sdk/include/Vendors/lepton/include"
    )

if(@OPENSIM_COPY_SIMBODY@)
    # Simbody headers have been copied to OpenSim.
    if(WIN32)
        set(_SIMBODY_INCLUDE_RELPATH "include")
    else()
        set(_SIMBODY_INCLUDE_RELPATH "simbody")
    endif()
    list(APPEND @CMAKE_PROJECT_NAME@_INCLUDE_DIRS
        "@PACKAGE_CMAKE_INSTALL_PREFIX@/@CMAKE_INSTALL_INCLUDEDIR@/SimTK/${_SIMBODY_INCLUDE_RELPATH}")
else()
    # Refer to Simbody's own headers.
    list(APPEND @CMAKE_PROJECT_NAME@_INCLUDE_DIRS "@Simbody_INCLUDE_DIR@")
endif()

# LIBRARIES
# ---------
include("@PACKAGE_OPENSIM_INSTALL_CMAKEDIR@/@CMAKE_PROJECT_NAME@Targets.cmake")

# The osimTools target uses all the other targets.
# This makes a distinction between release and debug libraries.
list(APPEND @CMAKE_PROJECT_NAME@_LIBRARIES osimTools)

check_required_components(OpenSim)
