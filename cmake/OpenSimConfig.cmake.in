# For an example of how to make your own CMake project that uses OpenSim,
# see SampleCMakeLists.txt.
#
# This config file defines the following variables:
#   @CMAKE_PROJECT_NAME@_FOUND               True if search for libraries and headers
#                               succeeds.
#   @CMAKE_PROJECT_NAME@_ROOT_DIR            The installation directory.
#   @CMAKE_PROJECT_NAME@_LIB_DIR             The directory containing libraries.
#                               Suitable for link_directories().
#   @CMAKE_PROJECT_NAME@_INCLUDE_DIRS        Suitable for include_directories().
#   @CMAKE_PROJECT_NAME@_LIBRARIES           Suitable for target_link_libraries().
#
# Note that these variables also contain the Simbody include directories
# and libraries that OpenSim was built with.
#
# Adapted from SimbodyConfig.cmake
#

list(APPEND @CMAKE_PROJECT_NAME@_ROOT_DIR "@CMAKE_INSTALL_PREFIX@")

# LIB_DIR
# -------
if(WIN32)
    set(_OPENSIM_LIB_RELPATH "sdk/lib")
else()
    set(_OPENSIM_LIB_RELPATH "lib")
endif()

list(APPEND @CMAKE_PROJECT_NAME@_LIB_DIR "@CMAKE_INSTALL_PREFIX@/${_OPENSIM_LIB_RELPATH}")

if(NOT WIN32)
    list(APPEND @CMAKE_PROJECT_NAME@_LIB_DIR "@Simbody_LIB_DIR@")
endif()

# INCLUDE_DIRS
# ------------
list(APPEND @CMAKE_PROJECT_NAME@_INCLUDE_DIRS
    "@CMAKE_INSTALL_PREFIX@/sdk/include"
    "@CMAKE_INSTALL_PREFIX@/sdk/include/Vendors/Lepton/include"
    )

if(WIN32)
    # Simbody headers have been copied to OpenSim.
    list(APPEND @CMAKE_PROJECT_NAME@_INCLUDE_DIRS
        "@CMAKE_INSTALL_PREFIX@/sdk/include/SimTK/include")
else()
    list(APPEND @CMAKE_PROJECT_NAME@_INCLUDE_DIRS "@Simbody_INCLUDE_DIR@")
endif()

# LIBRARIES
# ---------
include("@CMAKE_INSTALL_PREFIX@/@OPENSIM_INSTALL_CMAKE_DIR@/@CMAKE_PROJECT_NAME@Targets.cmake")

# The osimTools target uses all the other targets.
# This makes a distinction between release and debug libraries.
list(APPEND @CMAKE_PROJECT_NAME@_LIBRARIES osimTools)
